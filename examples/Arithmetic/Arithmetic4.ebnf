# A variant of an Arithmetic-grammar

#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = vertical             # implicit whitespace, denoted by ~, includes any number of line feeds
@ literalws   = right                # literals have implicit whitespace on the right hand side
@ comment     = /#.*/                # comments range from a '#'-character to the end of the line
@ ignorecase  = False                # literals and regular expressions are case-sensitive
@ drop        = whitespace           # drop anonymous whitespace and (anonymous) string literals

#######################################################################
#
#: Structure and Components
#
#######################################################################

expression = term | expression "+" term | expression "-" term
term       = factor | term "*" factor | term "/" factor
factor     = [ sign ] (num | var | group)
sign       = "+" | "-"
group      = "(" expression ")"

#######################################################################
#
#: "Leaf"-Expressions
#
#######################################################################

num     = /(?:0|(?:[1-9]\d*))(?:\.\d+)?/~
var     = /[A-Za-z]/~
