# Arithmetic-grammar

#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = vertical        # implicit whitespace, includes any number of line feeds
@ literalws   = right           # literals have implicit whitespace on the right hand side
@ comment     = /#.*/           # comments range from a '#'-character to the end of the line
@ ignorecase  = False           # literals and regular expressions are case-sensitive
@ drop        = whitespace      # drop anonymous whitespace and tokens

#######################################################################
#
#  Structure and Components
#
#######################################################################

expression = term  { ("+" | "-") term}
term       = factor  { ("*" | "/") factor}
factor     = [/-/] ( NUMBER | VARIABLE | group ) { VARIABLE | group }
group      = "(" expression ")"

#######################################################################
#
#  Regular Expressions
#
#######################################################################

NUMBER     = /(?:0|(?:[1-9]\d*))(?:\.\d+)?/~
VARIABLE   = /[A-Za-z]/~
