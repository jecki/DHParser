# EBNF-Syntax für MLW-Artikel

@ comment       =  /#.*(?:\n|$)/    # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
@ whitespace    =  /\s*/            # Auch Zeilenspränge zählen als Leerraum
@ literalws     =  both             # Leerraum vor und nach Literalen wird automatisch entfernt


Artikel         = [LEER]
                  §LemmaPosition  [ArtikelKopf]  §BedeutungsPosition  §Autorinfo
                  [LEER]  DATEI_ENDE


#### LEMMA-POSITION ##########################################################

LemmaPosition   = "LEMMA"  §Lemma  [LemmaVarianten]  §GrammatikPosition

Lemma           = [_tll]  WORT_KLEIN
_tll            = "*"

LemmaVarianten  = "VARIANTEN" §LVariante  { "," §LVariante }  [";" §LVZusatz]
LVariante       = ~/(?:[a-z]|-)+/~  # Buchstabenfolge mit Trennzeichen "-"
LVZusatz        = "sim."



#### GRAMMATIK-POSITION ######################################################

GrammatikPosition = "GRAMMATIK" §_wortart §";"  §Flexionen  [_genus]  {GrammatikVarianten} [";" | "."]

_wortart        = "nomen"  | "n." |
                  "verb"   | "v." |
                  "adverb" | "adv." |
                  "adjektiv" | "adj."


GrammatikVarianten = ";" §GVariante
GVariante       = Flexionen  [_genus]  ":"  Beleg

Flexionen       = Flexion { "," §Flexion }
Flexion         = /-?[a-z]+/~

_genus          = "maskulinum" | "m." |
                  "femininum" | "f." |
                  "neutrum" | "n."



#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = SchreibweisenPosition
SchreibweisenPosition =  "SCHREIBWEISE" §SWTyp ":" §SWVariante { "," §SWVariante}
SWTyp           = "script." | "script. fat-"
SWVariante      = Schreibweise ":" Beleg
Schreibweise    = "vizreg-" | "festregel(a)" | "fezdregl(a)" | "fat-"

Beleg           = Verweis
Verweis         = ~/>>\w+/~
VerweisZiel     = ~/<\w+>/~


#### BEDEUTUNGS-POSITION #####################################################

BedeutungsPosition = { "BEDEUTUNG" Bedeutung }+

Bedeutung       = Interpretamente | Bedeutungskategorie
Bedeutungskategorie = /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
Interpretamente = LateinischeBedeutung  DeutscheBedeutung  [Belege]
LateinischeBedeutung = "LAT" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = "DEU" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
Belege          = "BELEGE" { "*" EinBeleg }
EinBeleg        = { !(/\s*/ ("*" | "BEDEUTUNG" | "AUTOR" | "NAME" | "ZUSATZ")) /\s?.*/ }+
                  [Zusatz]
Zusatz          = "ZUSATZ" /\s?.*/


#### AUTOR/AUTORIN ###########################################################

Autorinfo       = ("AUTORIN" | "AUTOR") Name
Name            = WORT { WORT | /[A-ZÄÖÜÁÀ]\./ }


#### MISZELLANEEN ############################################################

WORT            = /[A-ZÄÖÜ]?[a-zäöüß]+/~
WORT_GROSS      = /[A-ZÄÖÜ][a-zäöüß]+/~
WORT_KLEIN      = /[a-zäöüß]+/~
LAT_WORT        = /[a-z]+/~
GROSSSCHRIFT    = /[A-ZÄÖÜ]+/~

LEER            = /\s*/
DATEI_ENDE      = !/./
NIEMALS         = /(?!.)/
