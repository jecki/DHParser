# EBNF-Syntax für MLW-Artikel


@ comment         =  /#.*/            # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
                                      # ohne das Zeilenende zu beinhalten
@ whitespace      =  /[\t ]*/         # Zeilensprünge zählen nicht als Leerraum
@ literalws       =  right            # Leerraum vor und nach Literalen wird automatisch entfernt


##############################################################################

Artikel           = [LZ]
                    §LemmaPosition
                    [ArtikelKopf]
                    BedeutungsPosition
                    ArtikelVerfasser
                    [LZ]  DATEI_ENDE


#### LEMMA-POSITION ##########################################################

LemmaPosition     = "LEMMA" [LZ] §Lemma TR [LemmaVarianten]
                    GrammatikPosition [EtymologiePosition]

Lemma             = [klassisch] [gesichert] LemmaWort
  klassisch       = "*"
  gesichert       = "$"

LemmaVarianten    = [LZ]
                    { LemmaWort §TR }+
                    [LemmaZusatz §ABS]

LemmaWort         = LAT_WORT_TEIL { ("|" | "-") LAT_WORT_TEIL }

LemmaZusatz       = "ZUSATZ" §lzs_typ
  lzs_typ         = /sim\./


## GRAMMATIK-POSITION ##

GrammatikPosition = "GRAMMATIK" [LZ] §wortart ABS flexion [genus] ABS
                    [GrammatikVarianten]

wortart          = "nomen"  | "n."
                 | "verb"   | "v."
                 | "adverb" | "adv."
                 | "adjektiv" | "adj."
                 | "praeposition" | "praep."

GrammatikVarianten = { [wortart ABS] flexion [genus]  ":"  Beleg §ABS }+

flexion          = FLEX { "," §FLEX }
FLEX             = /-?[a-z]+/~

genus            = "maskulinum" | "m."
                 | "femininum" | "f."
                 | "neutrum" | "n."


## ETYMOLOGIE-POSITION ##

EtymologiePosition = "ETYMOLOGIE" [LZ] EtymologieVarianten
EtymologieVarianten = { !([LZ] GROSSFOLGE) EtymologieVariante }+
EtymologieVariante = /.*/   # NOCH ZU VERFOLLSTÄNDIGEN


#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = SchreibweisenPosition
SchreibweisenPosition =  "SCHREIBWEISE" [LZ] §SWTyp ":" [LZ]
                         SWVariante { ABS SWVariante} [LZ]
SWTyp           = "script." | "script. fat-"
SWVariante      = Schreibweise ":" Beleg
Schreibweise    = ZEICHENFOLGE


#### BEDEUTUNGS-POSITION #####################################################

BedeutungsPosition = { "BEDEUTUNG" [LZ] §Bedeutung }+

Bedeutung       = (Interpretamente | Bedeutungskategorie) [Belege]
Bedeutungskategorie = /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~ [LZ]
Interpretamente = LateinischeBedeutung [LZ] §DeutscheBedeutung [LZ]
LateinischeBedeutung = SW_LAT /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = SW_DEU /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~

Belege          = "BELEGE" [LZ] (EinBeleg | { "*" EinBeleg })
EinBeleg        = { !([LZ] ("*" | GROSSFOLGE )) /\s*[^\n]*\s*/ }+ [Zusatz]
Zusatz          = "ZUSATZ" /\s*.*/ ABS


#### AUTOR/AUTORIN ###########################################################

ArtikelVerfasser = ("AUTORIN" | "AUTOR") Name
Name             = { NAME | NAMENS_ABKÜRZUNG }+


#### Schlüsselwörter #########################################################

SW_LAT           = "LATEINISCH" | "LAT"
SW_DEU           = "DEUTSCH" | "DEUT"
SW_GRIECH        = "GRIECHISCH" | "GRIECH" | "GRIE" | "GRI"


#### GENERISCHE UND ATOMARE AUSDRÜCKE ########################################

Beleg            = Verweis

Verweis          = ["->"] ZielName
VerweisZiel      = "[" ZielName "]"
ZielName         = BUCHSTABENFOLGE

NAMENS_ABKÜRZUNG = /[A-ZÄÖÜÁÀÂÓÒÔÚÙÛ]\./~
NAME             = /[A-ZÄÖÜÁÀÓÒÚÙÂÔÛ][a-zäöüßáàâóòôúùû]+/~

DEU_WORT         = /[A-ZÄÖÜ]?[a-zäöüß]+/~
DEU_GROSS        = /[A-ZÄÖÜ][a-zäöüß]+/~
DEU_KLEIN        = /[a-zäöüß]+/~
LAT_WORT         = /[a-z]+/~
LAT_WORT_TEIL    = /[a-z]+/
GROSSSCHRIFT     = /[A-ZÄÖÜ]+/~
GROSSFOLGE       = /[A-ZÄÖÜ][A-ZÄÖÜ][A-ZÄÖÜ]/   # drei Großbuchstaben in Folge

BUCHSTABENFOLGE  = /\w+/~
ZEICHENFOLGE     = /[\w()-]+/~

TR               = ABS | LZ                     # (beliebiger) Trenner
ABS              = /\s*;\s*/ | { ZWW }+         # Abschluss (durch Semikolon oder Zeilenwechsel)
# ZW               = /\n/~                        # Zeilenwechsel
LZ               = /\s+/                        # Leerzeichen oder -zeilen

ZW               = !LÜCKE ZEILENSPRUNG       # Zeilenwechsel, aber keine Leerzeile(n)
ZWW              = ZEILENSPRUNG [ LEERRAUM ] # mindestens ein Zeilenwechsel
LÜCKE            = KOMMENTARZEILEN LEERZEILE [LEERRAUM] # Leerraum mit mindestens einer echten Leerzeile
LEERRAUM         = { COMMENT__ | /\s+/ }+   # beliebiger horizontaler oder vertikaler Leerraum
LEERZEILE        = /[ \t]*(?:\n[ \t]*)+\n/ ~/\n?/ # eine oder mehrere echte LEERZEILEN
RZS              = /\s*?\n|$/               # Rückwärtiger Zeilensprung oder Textanfang

ZEILENSPRUNG     = /[ \t]*\n/~
KOMMENTARZEILEN  = { /[ \t]*\n?[ \t]*/ COMMENT__ }  # echte Kommentarzeilen

DATEI_ENDE       = !/./
NIEMALS          = /(?!.)/
