# EBNF-Syntax für MLW-Artikel

# TODO: Vervollständigen!!!!


@ comment         =  /#.*/            # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
                                      # ohne das Zeilenende zu beinhalten
@ whitespace      =  /[\t ]*/         # Zeilensprünge zählen nicht als Leerraum
@ literalws       =  right            # Leerraum vor und nach Literalen wird automatisch entfernt


##############################################################################

Artikel           = [LZ]
                    §{ LemmaPosition }+
                    [EtymologiePosition]
                    [ArtikelKopf]
                    BedeutungsPosition
                    [VerweisPosition]
                    { UnterArtikel }
                    ArtikelVerfasser
                    [LZ]  DATEI_ENDE



#### LEMMA-POSITION ##########################################################

LemmaPosition     = "LEMMA" [LZ] §Lemma TR [LemmaVarianten]
                    GrammatikPosition [Zusatz] [ABS]

Lemma             = [< klassisch | gesichert >] LemmaWort
  klassisch       = "*"
  gesichert       = "$"  # TODO: Noch fragen: Welches Zeichen?

LemmaWort         = LAT_WORT

LemmaVarianten    = [LZ]
                    { LemmaVariante §TR }+
                    [Zusatz]

LemmaVariante     = LAT_WORT_TEIL { "-" LAT_WORT_TEIL }


## GRAMMATIK-POSITION ##

GrammatikPosition = "GRAMMATIK" [LZ] §Grammatik ABS { GrammatikVariante §ABS }

Grammatik        = wortart §ABS flexion [genus]

wortart          = "nomen"  | "n."
                 | "verb"   | "v."
                 | "adverb" | "adv."
                 | "adjektiv" | "adj."
                 | "praeposition" | "praep."

flexion          = deklination | konjugation
deklination      = FLEX §"," FLEX
konjugation      = FLEX
FLEX             = /-?[a-z]+/~

genus            = "maskulinum" | "m."
                 | "femininum" | "f."
                 | "neutrum" | "n."

GrammatikVariante  = [wortart ABS] flexion [genus] DPP { Beleg }+



#### ETYMOLOGIE-POSITION #####################################################

EtymologiePosition = "ETYMOLOGIE" [LZ] { EtymologieVariante }+
EtymologieVariante = LAT | GRI [EtymologieBesonderheit] ["ETYM" Etymologie] DPP Beleg
EtymologieBesonderheit = FREITEXT
Etymologie         = FREITEXT



#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = <   SchreibweisenPosition | StrukturPosition | GebrauchPosition
                    | MetrikPosition | VerwechselungsPosition >

## Schreibweisen-Position ##

# TODO: Ggf. noch zu ergänzen um: Zusatz, Mehrere Tyen innerhalb der Schreibweisen-Position.

SchreibweisenPosition =  "SCHREIBWEISE" [LZ] { SWKategorie }+

SWKategorie      = SWTyp DPP [LZ] §(Varianten | { SWKategorie }+) [LZ]
SWTyp            = scriptfat | scriptform | script | form | gen | abl | OFFEN

scriptfat  = "script." "fat-"
scriptform = "script. " "form"
script     = "srcipt."
form       = "form"

gen        = "gen."
abl        = "abl."

OFFEN      = FREITEXT


#### STRUKTUR-POSITION #######################################################

StrukturPosition = "STRUKTUR" [LZ] { STKategorie }+
STKategorie      = STTyp DPP [LZ] §(Varianten | { STKategorie }+) [LZ]
STTyp            = "form." ZEICHENFOLGE


#### GEBRAUCH-POSITION #######################################################

GebrauchPosition = "GEBRAUCH"


#### METRIK-POSITION #########################################################

MetrikPosition = "METRIK"


#### VERWECHSLUNGS-POSITION ##################################################

VerwechslungsPosition  = "VERWECHSELBAR" [LZ] [Confunditur]
                         VerwechselungsVariante { ABS VerwechselungsVariante }+ [LZ]
Confunditur            = "confunditur c." DPP [LZ]
VerwechselungsVariante = !KATEGORIENZEILE Verwechselung DPP Beleg
Verwechselung          = ZEICHENFOLGE


#### ARTIKELKOPF POSITIONEN VARIANTEN ########################################

Varianten              = Variante { ABS Variante }
Variante               = !KATEGORIENZEILE Gegenstand DPP Beleg
Gegenstand             = ZEICHENFOLGE


#### BEDEUTUNGS-POSITION #####################################################
##############################################################################

BedeutungsPosition = { "BEDEUTUNG" [LZ] §Bedeutung }+

Bedeutung       = (Interpretamente | Bedeutungskategorie) [Belege]
Bedeutungskategorie = /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~ [LZ]
Interpretamente = LateinischeBedeutung [LZ] §DeutscheBedeutung [LZ]
LateinischeBedeutung = LAT /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = DEU /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~

Belege          = "BELEGE" [LZ] (EinBeleg | { "*" EinBeleg }) ABS
EinBeleg        = { !([LZ] "*" | SCHLUESSELWORT) /\s*[^\n]*/~ [ZW] }+ [Zusatz]


#### VERWEIS-POSITION #####################################################

VerweisPosition = "VERWEISE"


#### UNTER-ARTIKEL ########################################################

UnterArtikel = "UNTER-ARTIKEL"


#### AUTOR/AUTORIN ###########################################################

ArtikelVerfasser = ("AUTORIN" | "AUTOR") Name
Name             = { NAME | NAMENS_ABKÜRZUNG }+



#### Schlüsselwörter #########################################################

LAT = "LATEINISCH" | "LAT"
DEU = "DEUTSCH" | "DEU"
GRI = "GRIECHISCH" | "GRIECH" | "GRIE" | "GRI"

SCHLUESSELWORT   = { //~ /\n/ }+ !ROEMISCHE_ZAHL /[A-ZÄÖÜ]{3,}\s+/



#### ZUSATZ an verschiedenen Stellen der Struktur ############################

Zusatz       = "ZUSATZ" §{ zusatz_typ [TR] }+
  zusatz_typ = "adde" | "al" | "sim." | "saepe" | "vel-rarius" | "vel" | FREITEXT



#### BELEGE ##################################################################

Beleg            = (BelegQuelle BelegText) | BelegText | Verweis
BelegQuelle      = Autor DPP Werk SEM Stelle [SEM Datierung] [SEM Edition]
BelegText        = '"' FREITEXT '"'

Verweis          =  "->" ZielName
VerweisZiel      = "{" ZielName "}"
ZielName         = BUCHSTABENFOLGE

Autor     = FREITEXT
Werk      = FREITEXT
Stelle    = FREITEXT
Datierung = FREITEXT
Edition   = FREITEXT


#### GENERISCHE UND ATOMARE AUSDRÜCKE ########################################

NAMENS_ABKÜRZUNG = /[A-ZÄÖÜÁÀÂÓÒÔÚÙÛ]\./~
NAME             = /[A-ZÄÖÜÁÀÓÒÚÙÂÔÛ][a-zäöüßáàâóòôúùû]+/~

DEU_WORT         = /[A-ZÄÖÜ]?[a-zäöüß]+/~
DEU_GROSS        = /[A-ZÄÖÜ][a-zäöüß]+/~
DEU_KLEIN        = /[a-zäöüß]+/~
LAT_WORT         = /[a-z]+/~
LAT_WORT_TEIL    = /[a-z]+/
GROSSSCHRIFT     = /[A-ZÄÖÜ]+/~
ZAHL             = /\d+/~
ROEMISCHE_ZAHL   = /(?=[MDCLXVI])M*(C[MD]|D?C*)(X[CL]|L?X*)(I[XV]|V?I*)/~

SATZZEICHEN      = /(?:,(?!,))|(?:;(?!;))|(?::(?!:))|[.()\-]+/~  # div. Satzzeichen, aber keine doppelten ,, ;; oder ::

TEXTELEMENT      = DEU_WORT | ZAHL | ROEMISCHE_ZAHL
FREITEXT         = { TEXTELEMENT | /[.()\-\s]+/ | /,(?!,)\s*/ }+
ERW_FREITEXT     = { TEXTELEMENT | SATZZEICHEN | /\s+/ }+

BUCHSTABENFOLGE  = /\w+/~
ZEICHENFOLGE     = /[\w()-]+/~

TR               = ABS | LZ                  # (beliebiger) Trenner
ABS              = /\s*;;?\s*/ | { ZWW }+    # Abschluss (durch Semikolon oder Zeilenwechsel)
# ZW               = /\n/~                   # Zeilenwechsel
LZ               = /\s+/                     # Leerzeichen oder -zeilen
DPP              = /::?/~                    # Doppelpunkt als Trenner
SEM              = /;;?/~                    # Semikolon als Trenner

ZW               = !LÜCKE ZEILENSPRUNG       # Zeilenwechsel, aber keine Leerzeile(n)
ZWW              = ZEILENSPRUNG [ LEERRAUM ] # mindestens ein Zeilenwechsel
LÜCKE            = KOMMENTARZEILEN LEERZEILE [LEERRAUM] # Leerraum mit mindestens einer echten Leerzeile
LEERRAUM         = { COMMENT__ | /\s+/ }+   # beliebiger horizontaler oder vertikaler Leerraum
LEERZEILE        = /[ \t]*(?:\n[ \t]*)+\n/ ~/\n?/ # eine oder mehrere echte LEERZEILEN
RZS              = /\s*?\n|$/               # Rückwärtiger Zeilensprung oder Textanfang

ZEILENSPRUNG     = /[ \t]*\n/~
KOMMENTARZEILEN  = { /[ \t]*\n?[ \t]*/ COMMENT__ }  # echte Kommentarzeilen
KATEGORIENZEILE  = /[^:\n]+[:][ \t]*\n/     # Kategorienzeilen enthalten genau einen Doppelpunkt am Ende der Zeile

DATEI_ENDE       = !/./
NIEMALS          = /(?!.)/

DUMMY            = "EBNF-Grammatik an dieser Stelle noch nicht definiert!"
