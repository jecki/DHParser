# EBNF-Syntax für MLW-Artikel

@ comment       =  /#.*(?:\n|$)/    # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
@ whitespace    =  /[\t\r\ ]*/      # Auch Zeilensprünge zählen als Leerraum
@ literalws     =  both             # Leerraum vor und nach Literalen wird automatisch entfernt


Artikel         = [LEER]
                  §LemmaPosition  [ArtikelKopf]  §BedeutungsPosition  §Autorinfo
                  [LEER]  DATEI_ENDE


#### LEMMA-POSITION ##########################################################

LemmaPosition   = "LEMMA"  §Lemma  [LemmaVarianten]  §GrammatikPosition

Lemma           = [_tll]  WORT_KLEIN [LEER]
_tll            = "*"

LemmaVarianten  = "VARIANTEN" [LEER] §LVariante  { TRENNER LVariante }
                  [TRENNER LVZusatz] [TRENNER]
LVariante       = ~/(?:[a-z]|-)+/~      # Buchstabenfolge mit Trennzeichen "-"
LVZusatz        = "ZUSATZ" "sim."



#### GRAMMATIK-POSITION ######################################################

GrammatikPosition = "GRAMMATIK" [LEER] §_wortart §TRENNER §Flexionen [_genus]
                    {GrammatikVarianten} [TRENNER]

_wortart        = "nomen"  | "n." |
                  "verb"   | "v." |
                  "adverb" | "adv." |
                  "adjektiv" | "adj."


GrammatikVarianten = TRENNER GVariante
GVariante       = Flexionen  [_genus]  ":"  Beleg

Flexionen       = Flexion { "," §Flexion }
Flexion         = /-?[a-z]+/~

_genus          = "maskulinum" | "m." |
                  "femininum" | "f." |
                  "neutrum" | "n."



#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = SchreibweisenPosition
SchreibweisenPosition =  "SCHREIBWEISE" [LEER] §SWTyp ":" [LEER]
                         §SWVariante { TRENNER SWVariante} [LEER]
SWTyp           = "script." | "script. fat-"
SWVariante      = Schreibweise ":" Beleg
Schreibweise    = "vizreg-" | "festregel(a)" | "fezdregl(a)" | "fat-"

Beleg           = Verweis
Verweis         = ~/\w+/~
VerweisZiel     = ~/<\w+>/~


#### BEDEUTUNGS-POSITION #####################################################

BedeutungsPosition = { "BEDEUTUNG" [LEER] §Bedeutung [LEER] }+

Bedeutung       = (Interpretamente | Bedeutungskategorie) [Belege]
Bedeutungskategorie = /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~ [LEER]
Interpretamente = LateinischeBedeutung [LEER] §DeutscheBedeutung [LEER]
LateinischeBedeutung = "LAT" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = "DEU" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
Belege          = "BELEGE" [LEER] { "*" EinBeleg }
EinBeleg        = { !(/\s*/ ("*" | "BEDEUTUNG" | "AUTOR" | "NAME" | "ZUSATZ")) /\s*.*\s*/ }+
                  [Zusatz] [LEER]
Zusatz          = "ZUSATZ" /\s*.*/


#### AUTOR/AUTORIN ###########################################################

Autorinfo       = ("AUTORIN" | "AUTOR") Name
Name            = WORT { WORT | /[A-ZÄÖÜÁÀ]\./ }


#### MISZELLANEEN ############################################################

WORT            = /[A-ZÄÖÜ]?[a-zäöüß]+/~
WORT_GROSS      = /[A-ZÄÖÜ][a-zäöüß]+/~
WORT_KLEIN      = /[a-zäöüß]+/~
LAT_WORT        = /[a-z]+/~
GROSSSCHRIFT    = /[A-ZÄÖÜ]+/~

TRENNER         = /\s*;\s*/ | { ZSPRUNG }+
ZSPRUNG         = /\n/~

LEER            = /\s+/                     # horizontaler und(!) vertikaler Leerraum
DATEI_ENDE      = !/./
NIEMALS         = /(?!.)/

