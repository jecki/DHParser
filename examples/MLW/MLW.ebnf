# EBNF-Syntax für MLW-Artikel

@ comment       =  /#.*(?:\n|$)/    # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
@ whitespace    =  /\s*/            # Auch Zeilenspränge zählen als Leerraum
@ literalws     =  both             # Leerraum vor und nach Literalen wird automatisch entfernt


Artikel         = [LEER]
                  §LemmaPosition  [ArtikelKopf]  §BedeutungsPosition
                  [LEER]  DATEI_ENDE


#### LEMMA-POSITION ##########################################################

LemmaPosition   = "LEMMA"  §Lemma  [LemmaVarianten]  §GrammatikPosition

Lemma           = [_tll]  WORT_KLEIN
_tll            = "*"

LemmaVarianten  = "VARIANTEN" §LVariante  { "," §LVariante }  [";" §LVZusatz]
LVariante       = ~/(?:[a-z]|-)+/~  # Buchstabenfolge mit Trennzeichen "-"
LVZusatz        = "sim."



#### GRAMMATIK-POSITION ######################################################

GrammatikPosition = "GRAMMATIK" §_wortart §";"  §Flexionen  [_genus]  [GrammatikVarianten] ["."]

_wortart        = "nomen"  | "n." |
                  "verb"   | "v." |
                  "adverb" | "adv." |
                  "adjektiv" | "adj."


GrammatikVarianten = "("  §GVariante  { ";" §GVariante }  §")"
GVariante       = Flexionen  [_genus]  ":"  Beleg

Flexionen       = Flexion { "," §Flexion }
Flexion         = /-*[a-z]+/~

_genus          = "maskulinum" | "m." |
                  "femininum" | "f." |
                  "neutrum" | "n."



#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = SchreibweisenPosition
SchreibweisenPosition =  { "SCHREIBWEISE" §SWTyp ":" §SWVariante { "," §SWVariante} }
SWTyp           = "script."
SWVariante      = Schreibweise ":" Beleg
Schreibweise    = "vizreg-" | "festregel(a)" | "fezdregel(a)" | "fat-"



#### BEDEUTUNGS-POSITION #####################################################

BedeutungsPosition = { "BEDEUTUNG" Bedeutung }

Bedeutung       = Interpretamente | Bedeutungskategorie
Interpretatmente = LateinischeBedeutung  DeutscheBedeutung  [Belege]
LateinischeBedeutung = "LAT" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = "DEU" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
Belege          = "BELEGE" { "* " EinBeleg }
EinBeleg        = /(?:(?!(?:(?:BEDEUTUNG)|\*).)+/~

Beleg           = Verweis
Verweis         = ~/>>\w+/~
VerweisZiel     = ~/<\w+/>/~



#### MISZELLANEEN ############################################################

WORT            = /[A-ZÄÖÜ]*[a-zäöüÄÜÖß]+/~
WORT_KLEIN      = /[a-z]+/~
GROSSSCHRIFT    = /[A-ZÄÖÜ]+/~

LEER            = /\s*/
DATEI_ENDE      = !/./
NIEMALS         = /(?!.)/