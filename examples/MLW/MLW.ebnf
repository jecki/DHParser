# EBNF-Syntax für MLW-Artikel

@ comment         =  /#.*(?:\n|$)/    # Kommentare beginnen mit '#' und reichen bis zum Zeilenende
@ whitespace      =  /[\t ]*/         # Zeilensprünge zählen nicht als Leerraum
@ literalws       =  right            # Leerraum vor und nach Literalen wird automatisch entfernt


##############################################################################

Artikel           = [LZ]
                    §LemmaPosition
                    [ArtikelKopf]
                    §BedeutungsPosition
                    §Autorinfo
                    [LZ]  DATEI_ENDE


#### LEMMA-POSITION ##########################################################

LemmaPosition     = "LEMMA" [LZ]  §HauptLemma [LemmaVarianten] §GrammatikPosition

HauptLemma        = [klassisch] [gesichert] lemma
  klassisch       = "*"
  gesichert       = "$"

LemmaVarianten   = { (LZ|TR) lemma }+
                   [ (LZ|TR) LemmaZusatz] [LZ]

lemma           = LAT_WORT_TEIL { ("|" | "-") LAT_WORT_TEIL }

LemmaZusatz     = "ZUSATZ" lzs_typ
  lzs_typ       = "sim."


## GRAMMATIK-POSITION ##

GrammatikPosition = "GRAMMATIK" [LZ] §wortart §TR §Flexion [genus]
                    {GrammatikVariante} [TR]

wortart         = "nomen"  | "n." |
                  "verb"   | "v." |
                  "adverb" | "adv." |
                  "adjektiv" | "adj."

GrammatikVariante = TR GVariante
GVariante       = Flexionen  [genus]  ":"  Beleg

Flexionen       = Flexion { "," §Flexion }
Flexion         = /-?[a-z]+/~

genus           = "maskulinum" | "m." |
                  "femininum" | "f." |
                  "neutrum" | "n."



#### ARTIKEL-KOPF ############################################################

ArtikelKopf     = SchreibweisenPosition
SchreibweisenPosition =  "SCHREIBWEISE" [LZ] §SWTyp ":" [LZ]
                         §SWVariante { TR SWVariante} [LZ]
SWTyp           = "script." | "script. fat-"
SWVariante      = Schreibweise ":" Beleg
Schreibweise    = "vizreg-" | "festregel(a)" | "fezdregl(a)" | "fat-"

Beleg           = Verweis
Verweis         = ~/\w+/~
VerweisZiel     = ~/<\w+>/~


#### BEDEUTUNGS-POSITION #####################################################

BedeutungsPosition = { "BEDEUTUNG" [LZ] §Bedeutung }+

Bedeutung       = (Interpretamente | Bedeutungskategorie) [Belege]
Bedeutungskategorie = /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~ [LZ]
Interpretamente = LateinischeBedeutung [LZ] §DeutscheBedeutung [LZ]
LateinischeBedeutung = "LAT" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
DeutscheBedeutung = "DEU" /(?:(?![A-ZÄÖÜ][A-ZÄÖÜ]).)+/~
Belege          = "BELEGE" [LZ] { "*" EinBeleg }
EinBeleg        = { !([LZ] ("*" | "BEDEUTUNG" | "AUTOR" | "NAME" | "ZUSATZ"))
                    /\s*.*\s*/ }+
                  [Zusatz]
Zusatz          = "ZUSATZ" /\s*.*/ TR


#### AUTOR/AUTORIN ###########################################################

Autorinfo       = ("AUTORIN" | "AUTOR") Name
Name            = { NAME | NAMENS_ABKÜRZUNG }+


#### ATOMARE AUSDRÜCKE #######################################################

NAMENS_ABKÜRZUNG = /[A-ZÄÖÜÁÀÂÓÒÔÚÙÛ]\./~
NAME             = /[A-ZÄÖÜÁÀÓÒÚÙÂÔÛ][a-zäöüßáàâóòôúùû]+/~

DEU_WORT         = /[A-ZÄÖÜ]?[a-zäöüß]+/~
DEU_GROSS        = /[A-ZÄÖÜ][a-zäöüß]+/~
DEU_KLEIN        = /[a-zäöüß]+/~
LAT_WORT         = /[a-z]+/~
LAT_WORT_TEIL    = /[a-z]+/
GROSSSCHRIFT     = /[A-ZÄÖÜ]+/~

TR               = /\s*;\s*/ | { NEUE_ZEILE }+  # Trenner
NEUE_ZEILE       = /\n/~

LZ               = /\s+/                        # Leerzeichen oder -zeilen
DATEI_ENDE       = !/./
NIEMALS          = /(?!.)/

