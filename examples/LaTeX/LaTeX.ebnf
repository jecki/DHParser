# latex Grammar

@ whitespace = /[ \t]*\n?(?!\s*\n)[ \t]*/  # whitespace, including at most one linefeed
@ comment    = /%.*(?:\n|$)/

latexdoc   = preamble document
preamble   = { command }+

genericenv = beginenv sequence §endenv
beginenv   = "\begin" §( "{" name "}" )
endenv     = "\end" §( "{" ::name "}" )

name       = /\w+/~

comand     = cmdname [ config ] block
cmdname    = /\\\w+/
config     = "[" cfgtext §"]"

sequence   = { partext | parblock }

parblock   = "{" { partext | parblock } §"}"
block      = "{" { text | block } §"}"

partext    = text | PARSEP
text       = cfgtext | brackets
cfgtext    = chunk | escaped | WSPC

escaped    = /\\[%$&]/
brackets   = /[\[\]]/                   # left and right square brackets: [ ]
chunk      = /[^\\%$&\{\}\[\]\s\n]+/    # some piece of text excluding whitespace,
                                        # linefeed and special characters
WSPC       = /[ \t]*\n?(?!\s*\n)[ \t]*/ # whitespace, including at most one linefeed
LF         = /[ \t]*\n(?!\s*\n)/        # a linefeed, but not an empty line (i.e. par)
PARSEP     = /\s*\n\s*\n/               # at least one empty line, i.e.
                                        # [whitespace] linefeed [whitespace] linefeed

