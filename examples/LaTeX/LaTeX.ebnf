# latex Grammar

@ testing    = True
@ whitespace = /[ \t]*\n?(?!\s*\n)[ \t]*/  # whitespace, including at most one linefeed
@ comment    = /%.*(?:\n|$)/

latexdoc   = preamble document
preamble   = { command }+

document   = [PARSEP] { [PARSEP] paragraph } §EOF

genericenv = beginenv sequence §endenv
beginenv   = "\begin" §( "{" NAME "}" )
endenv     = "\end" §( "{" ::NAME "}" )

command    = CMDNAME [ config ] block
config     = "[" cfgtext §"]"

sequence   = { partext | parblock }

parblock   = "{" paragraph §"}"
block      = "{" { text | block } §"}"

paragraph  = { partext | parblock }

partext    = !blockcmd (text | PARSEP)
text       = cfgtext | BRACKETS
cfgtext    = word_sequence | ESCAPED | WSPC
word_sequence = { TEXTCHUNK WSPC }+

blockcmd   = "\subsection" | "\section" | "\chapter" | "\subsubsection"
             | "\paragraph" | "\subparagraph" | "\begin{enumerate}"
             | "\begin{itemize}" | "\item" | "\begin{figure}"

CMDNAME    = /\\\w+/~
NAME       = /\w+/~

ESCAPED    = /\\[%$&]/
BRACKETS   = /[\[\]]/                   # left or right square bracket: [ ]
TEXTCHUNK  = /[^\\%$&\{\}\[\]\s\n]+/    # some piece of text excluding whitespace,
                                        # linefeed and special characters
WSPC       = /[ \t]*\n?(?![ \t]*\n)[ \t]*/ # whitespace, including at most one linefeed
LF         = /[ \t]*\n(?!\s*\n)/        # a linefeed, but not an empty line (i.e. par)
PARSEP     = /\n[ \t]*(?=\n)/~          # at least one empty line, i.e.
                                        # [whitespace] linefeed [whitespace] linefeed

EOF        = !/./
