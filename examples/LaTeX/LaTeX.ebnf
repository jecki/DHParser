# latex Grammar

@ testing    = True
@ whitespace = /[ \t]*(?:\n(?![ \t]*\n)[ \t]*)?/  # optional whitespace, including at most one linefeed
@ comment    = /%.*(?:\n|$)/

latexdoc   = preamble document
preamble   = { command }+

document   = [PARSEP] { [PARSEP] paragraph } §EOF

blockenv   = beginenv sequence §endenv

parblock   = "{" sequence §"}"

sequence   = { paragraph [PARSEP] }+
paragraph  = { !blockcmd (command | block | text) }+

inlineenv  = beginenv { command | block | text }+ endenv
beginenv   = "\begin{" §NAME §"}"
endenv     = "\end{" §::NAME §"}"

command    = CMDNAME [[ config ] block]
config     = "[" cfgtext §"]"
block      = "{" { text | block } §"}"

text       = { cfgtext | (BRACKETS //~) }+
cfgtext    = { word_sequence | (ESCAPED //~) }+
word_sequence = { TEXTCHUNK //~ }+

blockcmd   = "\subsection" | "\section" | "\chapter" | "\subsubsection"
             | "\paragraph" | "\subparagraph" | "\begin{enumerate}"
             | "\begin{itemize}" | "\item" | "\begin{figure}"

CMDNAME    = /\\\w+/~
NAME       = /\w+/~

ESCAPED    = /\\[%$&]/
BRACKETS   = /[\[\]]/                       # left or right square bracket: [ ]
TEXTCHUNK  = /[^\\%$&\{\}\[\]\s\n]+/        # some piece of text excluding whitespace,
                                            # linefeed and special characters
WSPC       = /[ \t]+/                       # (horizontal) whitespace
LF         = !PARSEP /[ \t]*\n[ \t]*/       # LF but not an empty line
PARSEP     = /[ \t]*(?:\n[ \t]*)+\n[ \t]*/  # at least one empty line, i.e.
                                            # [whitespace] linefeed [whitespace] linefeed

EOF        = !/./
