# LaTeX-Grammar for DHParser

@ testing    = True
@ whitespace = /[ \t]*(?:\n(?![ \t]*\n)[ \t]*)?/    # optional whitespace, including at most one linefeed
@ comment    = /%.*(?:\n|$)/


latexdoc       = preamble document
preamble       = { command }+

document       = [PARSEP] "\begin{document}" [PARSEP]
                 frontpages [PARSEP]
                 (Chapters | Sections) [PARSEP]
                 [Bibliography] [Index] [PARSEP]
                 "\end{document}" [PARSEP] §EOF
frontpages     = sequence


#######################################################################
#
# document structure
#
#######################################################################

Chapters       = { Chapter [PARSEP] }+
Chapter        = "\Chapter" block [PARSEP] { sequence | Sections }

Sections       = { Section [PARSEP] }+
Section        = "\Section" block [PARSEP] { sequence | SubSections }

SubSections    = { SubSection [PARSEP] }+
SubSection     = "\SubSection" block [PARSEP] { sequence | SubSubSections }

SubSubSections = { SubSubSection [PARSEP] }+
SubSubSection  = "\SubSubSection" block [PARSEP] { sequence | Paragraphs }

Paragraphs     = { Paragraph [PARSEP] }+
Paragraph      = "\paragraph" block [PARSEP] { sequence | SubParagraphs }

SubParagraphs  = { SubParagraph [PARSEP] }+
SubParagraph   = "\subparagpaph" block [PARSEP] { sequence }

Bibliography   = "\bibliography" block [PARSEP]
Index          = "\printindex" [PARSEP]


#######################################################################
#
# document content
#
#######################################################################


#### block environments ####

# TODO: ambiguity between generic bock envieronments and generic inline environments

block_environment   = known_environment | generic_environment
known_environment   = itemize | enumerate | figure | table | quotation
                    | verbatim
generic_environment = begin_environment sequence §end_environment

itemize             = "\begin{itemize}" [PARSEP] { item } §"\end{itemize}"
enumerate           = "\begin{enumerate}" [PARSEP] {item } §"\end{enumerate}"
item                = "\item" [PARSEP] sequence

figure              = "\begin{figure}" sequence "\end{figure}"
quotation           = ("\begin{quotation}" sequence "\end{quotation}")
                    | ("\begin{quote}" sequence "\end{quote}")
verbatim            = "\begin{verbatim}" sequence "\end{verbatim}"
table               = "\begin{tabular}" table_config sequence "\end{tabular}"
table_config        = "{" /[lcr|]+/~ "}"


#### paragraphs and sequences of paragraphs ####

block_of_paragraphs = /{/ sequence §/}/
sequence            = { (paragraph | block_environment ) [PARSEP] }+

paragraph           = { !blockcmd text_elements //~ }+
text_elements       = command | text | block | inline_environment


#### inline enivronments ####

inline_environment  = known_inline_env | generic_inline_env
known_inline_env    = inline_math
generic_inline_env  = begin_environment { text_elements }+ §end_environment
begin_environment   = "\begin{" §NAME §"}"
end_environment     = "\end{" §::NAME §"}"

inline_math         = "$" MATH "$"


#### commands ####

command             = known_command | generic_command
known_command       = footnote | includegraphics | caption
generic_command     = CMDNAME [[ //~ config ] //~ block ]

footnote            = "\footnote" block_of_paragraphs
includegraphics     = "\includegraphics" config block
caption             = "\caption" block

#######################################################################
#
# low-level text and character sequences
#
#######################################################################

config     = "[" cfgtext §"]"
block      = /{/ { text_elements } §/}/

text       = { cfgtext | (BRACKETS //~) }+
cfgtext    = { word_sequence | (ESCAPED //~) }+
word_sequence = { TEXTCHUNK //~ }+

blockcmd   = /[\\]/ ( ( "begin{" | "end{" )
                      ( "enumerate" | "itemize" | "figure" | "quote"
                      | "quotation" | "tabular") "}"
                    | structural)

structural = "subsection" | "section" | "chapter" | "subsubsection"
           | "paragraph" | "subparagraph" | "item"


#######################################################################
#
# Primitives
#
#######################################################################

CMDNAME    = /\\(?:(?!_)\w)+/~
NAME       = /\w+/~
MATH       = /[\w_^{}[\]]*/~

ESCAPED    = /\\[%$&_\/]/
BRACKETS   = /[\[\]]/                       # left or right square bracket: [ ]
TEXTCHUNK  = /[^\\%$&\{\}\[\]\s\n]+/        # some piece of text excluding whitespace,
                                            # linefeed and special characters
WSPC       = /[ \t]+/                       # (horizontal) whitespace
LF         = !PARSEP /[ \t]*\n[ \t]*/       # LF but not an empty line
PARSEP     = /[ \t]*(?:\n[ \t]*)+\n[ \t]*/  # at least one empty line, i.e.
                                            # [whitespace] linefeed [whitespace] linefeed
EOF        = !/./
