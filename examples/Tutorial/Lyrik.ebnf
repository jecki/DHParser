gedicht           = bibliographisches { LEERZEILE }+ [serie] §titel §text §ENDE

bibliographisches = autor §"," [NZ] werk §"," [NZ] ort §"," [NZ] jahr §"."
autor             = namenfolge [verknüpfung]
werk              = wortfolge ["." §untertitel] [verknüpfung]
untertitel        = wortfolge [verknüpfung]
ort               = wortfolge [verknüpfung]
jahr              = JAHRESZAHL

serie             = !(titel vers NZ vers) { NZ zeile }+ { LEERZEILE }+
titel             = NZ zeile { LEERZEILE }+
zeile             = { ZEICHENFOLGE }+

text              = { strophe {LEERZEILE}+ }+
strophe           = { NZ vers }+
vers              = { ZEICHENFOLGE }+

wortfolge         = { WORT }+
namenfolge        = { NAME }+
verknüpfung       = "<" ziel ">"
ziel              = ZEICHENFOLGE

WORT              = /\w+/~
NAME              = /\w+\.?/~
ZEICHENFOLGE      = /[^ \n<>]+/~
LEER              = /[ \t]+/
NZ                = /\n/~
LEERZEILE         = /\n[ \t]*(?=\n)/~
JAHRESZAHL        = /\d\d\d\d/~
ENDE              = !/./
