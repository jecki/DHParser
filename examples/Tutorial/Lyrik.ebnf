gedicht           = bibliographisches { LEERZEILE }+ [serie] §titel §text /\s*/ §ENDE

bibliographisches = autor §"," [NZ] werk §"," [NZ] ort §"," [NZ] jahr §"."
autor             = namenfolge [verknüpfung]
werk              = wortfolge ["." §untertitel] [verknüpfung]
untertitel        = wortfolge [verknüpfung]
ort               = wortfolge [verknüpfung]
jahr              = JAHRESZAHL

wortfolge         = { WORT }+
namenfolge        = { NAME }+
verknüpfung       = "<" ziel ">"
ziel              = ZEICHENFOLGE

serie             = !(titel vers NZ vers) { NZ zeile }+ { LEERZEILE }+

titel             = { NZ zeile}+ { LEERZEILE }+
zeile             = { ZEICHENFOLGE }+

text              = { strophe {LEERZEILE} }+
strophe           = { NZ vers }+
vers              = { ZEICHENFOLGE }+

WORT              = /\w+/~
NAME              = /\w+\.?/~
ZEICHENFOLGE      = /[^ \n<>]+/~
NZ                = /\n/~
LEERZEILE         = /\n[ \t]*(?=\n)/~
JAHRESZAHL        = /\d\d\d\d/~
ENDE              = !/./
