# ts2dataclass-grammar
# for typescript see: https://www.typescriptlang.org/docs/
# for examples of typescript-interfaces
# see: https://microsoft.github.io/language-server-protocol/specifications/specification-current/

#######################################################################
#
#  EBNF-Directives
#
#######################################################################

@ whitespace  = /\s*/           # implicit whitespace, includes linefeed, but not empty lines
@ literalws   = right           # literals have implicit whitespace on the right hand side
@ comment     = /(?:\/\/.*)|(?:\/\*(?:.|\n)*?\*\/)/   # /* ... */ or // to EOL
@ ignorecase  = False           # literals and regular expressions are case-sensitive
@ reduction   = merge_treetops  # anonymous nodes are being reduced where possible
@ disposable  = INT, NEG, FRAC, DOT, EXP, EOF, _type, _literal, _name, _array_ellipsis,
                _top_level_assignment, _top_level_literal
@ drop        = whitespace, strings, EOF, _array_ellipsis, _top_level_assignment,
                _top_level_literal


#######################################################################
#
#:  Typescript Document
#
#######################################################################

document    = ~ { interface | type_alias | namespace | enum | const
                | declaration ";" | _top_level_assignment
                | _array_ellipsis | _top_level_literal } EOF


#######################################################################
#
#:  Interfaces
#
#######################################################################

interface   = ["export"] "interface" §identifier [type_parameter]
              [extends] declarations_block
  type_parameter = "<" identifier ">"
extends     = "extends" identifier { "," identifier}
type_alias  = ["export"] "type" §identifier "=" types ";"

declarations_block = "{"  [declaration { ";" declaration }
                           [";" map_signature] [";"]] "}"
declaration = [qualifier] identifier [optional] [":" types]
  qualifier = "readonly"
  optional  = "?"
  index_signature = "[" identifier (":" | "in" "keyof") _type "]"
types       = _type { "|" _type }
_type        = array_of | basic_type | type_name | "(" types ")"
             | mapped_type | declarations_block | type_tuple | _literal
type_name   = identifier
array_of    = (basic_type | "(" types ")" | identifier) "[]"
type_tuple  = "[" _type {"," _type} "]"
mapped_type = "{" map_signature [";"] "}"
map_signature = index_signature ":" types


#######################################################################
#
#:  Namespaces
#
#######################################################################

namespace   = ["export"] "namespace" §identifier
              "{" { const } "}"

#######################################################################
#
#:  Enums
#
#######################################################################

enum        = ["export"] "enum" [identifier] §"{" item { "," item } "}"
  item      = identifier ["=" _literal]


#######################################################################
#
#: Consts
#
#######################################################################

const       = ["export"] "const" §declaration "=" (_literal | identifier) ";"
_top_level_assignment = assignment
assignment  = variable "=" _literal ";"

#######################################################################
#
#: literals
#
#######################################################################

_array_ellipsis = _literal { "," _literal }

_top_level_literal = _literal
_literal    = number | string | array | object
number     = INT FRAC EXP ~
string     = /"[^"\n]*"/~ | /'[^'\n]*'/~
array      = "[" [ _literal { "," _literal } ] "]"
object     = "{" [ association { "," association } ] "}"
 association = _name ":" _literal
_name       = identifier | '"' identifier '"'

#######################################################################
#
#: Keywords
#
#######################################################################

basic_type   = (`object` | `array` | `string` | `number` | `boolean` | `null`
               | `integer` | `uinteger` ) ~


#######################################################################
#
#: Entities
#
#######################################################################

variable   = identifier { `.` identifier }
identifier = /(?!\d)\w+/~

INT         = [NEG] ( /[1-9][0-9]+/ | /[0-9]/ )
NEG         = `-`
FRAC        = [ DOT /[0-9]+/ ]
DOT         = `.`
EXP         = [ (`E`|`e`) [`+`|`-`] /[0-9]+/ ]

EOF        =  !/./        # no more characters ahead, end of file reached
