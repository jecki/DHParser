# BibTeX-Grammar

#######################################################################
#
#  EBNF-Directives
#
######################################################################

@ testing     = True
@ whitespace  = /\s*/
@ ignorecase  = True
@ comment     = /%.*(?:\n|$)/


#######################################################################
#
#  Bib-file Structure and Components
#
#######################################################################

bibliography = { preamble | comment | entry }

preamble      = "@Preamble{" /"/ pre_code /"/~ ยง"}"
pre_code      = { /[^"%]+/ | /%.*\n/ }

comment       = "@Comment{" text ยง"}"

entry         = /@/ type "{" key { "," field ยง"=" content } ยง"}"
type          = WORD
key           = NO_BLANK_STRING
field         = WORD_
content       = "{" text "}" | plain_content

plain_content = COMMA_TERMINATED_STRING
text          = NESTED_BRACES_STRING


#######################################################################
#
#  Regular Expressions
#
#######################################################################

WORD          = /\w+/
WORD_         = /\w+/~
NO_BLANK_STRING         = /[^ \t\n,%]+/~
COMMA_TERMINATED_STRING = { /[^,%]+/ | /(?=%)/~ }
NESTED_BRACES_STRING    = { /(?:\\\{|\\\}|[^}%])+/~ | /\{/ NESTED_BRACES_STRING /\}/ | /(?=%)/~ }
