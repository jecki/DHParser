# BibTeX-Grammar


#######################################################################
#
#  EBNF-Directives
#
######################################################################

@ literalws   = right
@ whitespace  = /\s*/
@ ignorecase  = True
@ comment     = /%[^\n]*\n/


#######################################################################
#
#  Bib-file Structure and Components
#
#######################################################################

bibliography = { preamble | comment | entry }  ~ EOF

preamble      = "@Preamble{" /"/ pre_code /"/~ ยง"}"
pre_code      = { /[^"%]+/ | /%.*\n/ }

comment       = "@Comment{" text ยง"}"

entry         = /@/ type "{" key { "," field ยง"=" content } [","] ยง"}"
type          = WORD
key           = NO_BLANK_STRING
field         = WORD
content       = "{" text "}" | plain_content

plain_content = COMMA_TERMINATED_STRING
text          = { CONTENT_STRING | "{" text "}" }


#######################################################################
#
#  Regular Expressions
#
#######################################################################

WORD          = /\w+/~
NO_BLANK_STRING         = { /[^ \t\n,%&_]+/ | ESC }+ ~
COMMA_TERMINATED_STRING = { /[^,%&_ \t]+/ | ESC | WS }
CONTENT_STRING          = { /[^{}%&_ \t]+/ | ESC | WS }+

ESC           = <-&/\\/ /[%&_]/          # escaped characters
WS            = &/[ \t]*%/~ | /[ \t]+/  # non-trailing whitespace

EOF           =  !/./
