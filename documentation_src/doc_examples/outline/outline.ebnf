# outline-grammar

#  EBNF-Directives

@ whitespace  = /[ \t]*/  # only horizontal whitespace, no linefeeds
@ reduction   = merge     # simplify tree as much as possible
@ disposable  = WS, EOF, LINE, GAP, LLF, L, # LF,
                CHARS, TEXT, ESCAPED, inner_emph, inner_bold, blocks
@ drop        = WS, EOF, GAP, backticked, whitespace

#:  Outline

document = main [WS] §EOF

main  = [WS] `#` !`#` ~ heading [WS blocks] { WS (§ &(section|EOF)) section }
section  = `##` !`#` ~ heading [WS blocks] { WS (§ &(section|subsection|EOF)) subsection }
subsection  = `###` !`#` ~ heading [WS blocks]
              { WS (§ &(section|subsection|subsubsection|EOF)) subsubsection }
subsubsection  = `####` !`#` ~ heading [WS blocks]
                 { WS (§ &(section|subsection|subsubsection|s5section|EOF)) s5section }
s5section  = `#####` !`#` ~ heading [WS blocks]
             { WS (§ &(section|subsection|subsubsection|s5section|s6section|EOF)) s6section }
s6section  = `######` !`#` ~ heading [WS blocks]

@document_error = '', "Dateiende erwartet!"
@main_error = /(?=#)/, "2010:Falsche Gliederungsebene"
@section_error = /(?=#)/, "2010:Falsche Gliederungsebene"
@subsection_error = /(?=#)/, "2010:Falsche Gliederungsebene"
@subsubsection_error = /(?=#)/, "2010:Falsche Gliederungsebene"
@s5section_error = /(?=#)/, "2010:Falsche Gliederungsebene"

@document_skip = /(?:.|\n)*/
@main_skip = ({!(section|EOF) /#*[^#]*/ })
@section_skip = ({!(section|subsection|EOF) /#*[^#]*/ })
@subsection_skip = ({!(section|subsection|subsubsection|EOF) /#*[^#]*/ })
@subsubsection_skip = ({!(section|subsection|subsubsection|s5section|EOF) /#*[^#]*/ })
@s5section_skip = ({!(section|subsection|subsubsection|s5section|s6section|EOF) /#*[^#]*/ })

heading = LINE

#: block

blocks  = !is_heading markup { GAP !is_heading markup }
is_heading = /##?#?#?#?#?(?!#)/

markup    = [indent] (text | bold | emphasis) { [LLF] (text | bold | emphasis) }
indent    = /[ \t]+(?=[^\s])/
text      = (TEXT | ESCAPED) { [LLF] (TEXT | ESCAPED) }

#: inline-elements

emphasis  = `*` !`*` §inner_emph `*`
          | `_` !`_` §inner_emph `_`
bold      = `**` §inner_bold `**`
          | `__` §inner_bold `__`

inner_emph = [~ &/[*_]/] (text | bold) { [LLF] (text | bold) } [<-&/[*_]/ ~]
inner_bold = [~ &/[*_]/] (text | emphasis) { [LLF] (text | emphasis) } [<-&/[*_]/ ~]

#:  Regular Expressions

LINE      = /[^\n]+/                # everything up to the next line-feed
TEXT      = CHARS { LLF CHARS }
CHARS     = /[^\s\\_*]+/
ESCAPED   = `\` /./
LLF       = L | LF
L         = /[ \t]/~                # significant whitespace
LF        = /[ \t]*\n[ \t]*(?!\n)/  #  single line-feed
# LFN       = ~/\n(?![ \t]*\n)/~    # normalized single line-feed
PARSEP    = ~/\n/ ~/\n/
WS        = GAP                     # same as GAP, but will be dropped
GAP       = /(?:[ \t]*\n)+/    # any ws at line-end and all following empty lines
EOF       =  !/./  # no more characters ahead, end of file reached
