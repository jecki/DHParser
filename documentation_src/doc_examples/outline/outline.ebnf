# outline-grammar

#  EBNF-Directives

@ whitespace  = /[ \t]*/  # only horizontal whitespace, no linefeeds
@ reduction   = merge     # simplify tree as much as possible
@ disposable  = WS, EOF, LINE, GAP, LLF, L, # LF,
                CHARS, TEXT, ESCAPED, inner_emph, inner_bold, blocks
@ drop        = WS, EOF, GAP, backticked, whitespace

#: outline

document = main [WS] §EOF

main  = [WS] `#` !`#` ~ heading [WS blocks] { WS (§&main_expect) section }
section  = `##` !`#` ~ heading [WS blocks] { WS (§&section_expect) subsection }
subsection  = `###` !`#` ~ heading [WS blocks] { WS (§&subsection_expect) subsubsection }
subsubsection  = `####` !`#` ~ heading [WS blocks] { WS (§&subsubsection_expect) s5section }
s5section  = `#####` !`#` ~ heading [WS blocks] { WS (§&s5section_expect) s6section }
s6section  = `######` !`#` ~ heading [WS blocks]

heading = LINE


# error-handling for outline

main_expect = section | EOF
section_expect = section | subsection | EOF
subsection_expect = section | subsection | subsubsection | EOF
subsubsection_expect = section | subsection | subsubsection | s5section | EOF
s5section_expect = section | subsection | subsubsection | s5section | s6section | EOF

@document_error = '', "End of file expected!"
@main_error = /(?=#)/, "2010:Bad nesting of headings"
@section_error = /(?=#)/, "2010:Bad nesting of headings"
@subsection_error = /(?=#)/, "2010:Bad nesting of headings"
@subsubsection_error = /(?=#)/, "2010:Bad nesting of headings"
@s5section_error = /(?=#)/, "2010:Bad nesting of headings"

@document_skip = /(?!.|\n)/
@main_skip = ({!main_expect until_heading})
@section_skip = ({!section_expect until_heading})
@subsection_skip = ({!subsection_expect until_heading})
@subsubsection_skip = ({!subsubsection_expect until_heading})
@s5section_skip = ({!s5section_expect until_heading})

until_heading = /#*[^#]*/


#: blocks

blocks  = !is_heading markup { GAP !is_heading markup }
is_heading = /##?#?#?#?#?(?!#)/

markup    = [indent] (text | bold | emphasis) { [LLF] (text | bold | emphasis) }
indent    = /[ \t]+(?=[^\s])/
text      = (TEXT | ESCAPED) { [LLF] (TEXT | ESCAPED) }


#: inline-elements

emphasis  = `*` !`*` §inner_emph `*`
          | `_` !`_` §inner_emph `_`
bold      = `**` §inner_bold `**`
          | `__` §inner_bold `__`

inner_emph = [~ &/[*_]/] (text | bold) { [LLF] (text | bold) } [<-&/[*_]/ ~]
inner_bold = [~ &/[*_]/] (text | emphasis) { [LLF] (text | emphasis) } [<-&/[*_]/ ~]


#:  Regular Expressions

LINE      = /[^\n]+/                # everything up to the next line-feed
TEXT      = CHARS { LLF CHARS }
CHARS     = /[^\s\\_*]+/
ESCAPED   = `\` /./
LLF       = L | LF
L         = /[ \t]/~                # significant whitespace
LF        = /[ \t]*\n[ \t]*(?!\n)/  #  single line-feed
# LFN       = ~/\n(?![ \t]*\n)/~    # normalized single line-feed
PARSEP    = ~/\n/ ~/\n/
WS        = GAP                     # same as GAP, but will be dropped
GAP       = /(?:[ \t]*\n)+/    # any ws at line-end and all following empty lines
EOF       =  !/.|\n/  # no more characters ahead, end of file reached
