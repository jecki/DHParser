# outline-grammar

#  EBNF-Directives

@ whitespace  = /[ \t]*/  # only horizontal whitespace, no linefeeds
@ reduction   = merge     # simplify tree as much as possible
@ disposable  = WS, EOF, LINE
@ drop        = WS, EOF, backticked

#:  Outline

document = main [WS] Â§EOF

main  = [WS] `#` !`#` ~ heading { [WS] section }
section  = `##` !`#` ~ heading { [WS] subsection }
subsection  = `###` !`#` ~ heading { [WS] subsubsection }
subsubsection  = `####` !`#` ~ heading { [WS] s5section }
s5section  = `#####` !`#` ~ heading { [WS] s6section }
s6section  = `######` !`#` ~ heading

heading = LINE

#: blocks

blocks  = !is_heading LINE { WS !is_heading LINE }
is_heading = /##?#?#?#?#?(?!#)/

#:  Regular Expressions

LINE      = /[^\n]+/         # everything up to the next linefeed
WS        = LFF
LFF       = /(?:[ \t]*\n)+/  # any ws at line-end and all following empty lines
EOF       =  !/./  # no more characters ahead, end of file reached
